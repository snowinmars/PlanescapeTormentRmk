init 10 python:
    from dlgs.introduction_logic import IntroductionLogic
    introductionLogic = IntroductionLogic(renpy.store.global_settings_manager)


label quick_setup_as_mage:
    $ introductionLogic.setup_new_life_as_mage()
    return


label introduction:
    snowinmars 'Привет. Сразу вопрос.'

    menu:
        'Я не боюсь спойлеров':
            $ introductionLogic.set_can_spoiler_true()
            jump introduction_1
        'Не надо мне спойлерить игру':
            jump introduction_1


label introduction_1:
    snowinmars 'Я переношу игру на новый движок по нескольким причинам.'
    snowinmars 'Первая - я очень люблю эту игру.'

    if introductionLogic.can_spoiler:
        snowinmars 'Вторая - Planescape:Torment - деконструкция РПГ.'
        snowinmars 'Если кратко: деконструкция - когда разбираешь что-то на части, меняешь их и собираешь целое заново.'
        snowinmars 'Результат выглядит похожим на оригинал: те же элементы, можно даже не увидеть разницы.'
        snowinmars 'Но ты живёшь с деконструкцией минуту, десять, сто - и выясняется, что результат совсем не похож на оригинал. Он вообще другой. Как мозаика, которую можно собрать двумя способами.'
        snowinmars 'Planescape:Torment - деконструкция РПГ. Здесь есть все те же элементы, что и в РПГ: персонаж, прокачка, навыки, днд, пара диалогов.'
        snowinmars 'Но когда начинаешь погружаться в игру - ты понимаешь, что боёвка и диалоговая система поменяны ролями.'
        snowinmars 'Бои происходят внутри бесед, а стучать дубинкой и бросать кубики - это так, между делом.'
        snowinmars 'Я подумал: а, собсно, почему бы не заострить: я выброшу всё, кроме повествования. Оно будет работать?'
    else:
        snowinmars 'Вторая…'
        snowinmars 'Страх спойлеров растёт из того, что произведению нечем тебя удивить, кроме внезапности.'
        snowinmars 'Искусство нельзя заспойлерить. Сюжет "Ромео и Джульетты" излагается на первой странице книги - потому что не в нём смысл.'
        snowinmars 'Тот сюжет был бородат ещё во времена Шекспира - но он пересказал его таким, что мы помним канву спустя половину тысячелетия. Поэтому и спойлерит его сразу.'
        snowinmars 'Не в том смысл, что he was a boy, she was a girl. Хотя ши, bezuslovno, воз.'
        snowinmars 'Если тебе так спокойнее - хорошо. После прохождения игры ты поймёшь, что я прав.'

    snowinmars 'Третья - сколько я не предлагаю друзьям сыграть в PsT - все вежливо сливаются.'
    snowinmars 'Потому что видят днд, кубики, устаревшую - для кого-то - графику, слабый интерфейс - и не особо понимают, зачем им это надо. Потому что я посоветовал?'
    snowinmars 'Кому-то этого хватит. Большинству - нет.'
    snowinmars 'Удобные движок и интерфейс - ящитаю - могут сгладить кривую входа. Если их корректно написать)'

    snowinmars 'Так, важное.'
    snowinmars 'Это - технодемка.'
    snowinmars 'Я заведомо не парился: ни о качестве кода, ни о графике. Моя задача - понять, можно ли перенести игру в принципе на RenPy.'
    snowinmars 'Проекты вроде Kill the Princess показывают, что ягоды в ягодицах у RenPy вполне есть.'
    snowinmars 'Основная сложность - диалоговая система. Если стандартная визновелла - это простыня текста, выбор - и снова текст, то в PsT игрок делает выбор после пары фраз.'
    snowinmars 'И снова, и снова. Монологов в привычном смысле тут нет - ну, кроме вот этого, который я сейчас пишу.'
    snowinmars 'Это создаёт чудовищно сложное диалоговое дерево. Я не был уверен, что на RenPy им можно удобно управлять. Я не был уверен, что его можно написать.'
    snowinmars 'Благодаря незнакомому мне человеку - Argent77 - я смог упростить перенос диалогового ктулху до тривиальных действий.'
    snowinmars 'Его расширение MassExporter для NearInfinity позволяет получить всю необходимую информацию в древнем, но формате. Остальное оказалось делом техники.'
    snowinmars 'Ссылка на его форк: https://github.com/Argent77/NearInfinity'
    snowinmars 'Как оказалось, RenPy поддерживает виртуальные label - на них построены графические менюшки. И после небольшой боли - даже какие-то сервисы.'
    snowinmars 'Питоновский код сгенерен deepseek r1, за что ему большое спасибо. Несмотря на десять с хреном лет в разработке, я не могу понять красоту этого языка. И уже не хочу понимать.'
    snowinmars 'Спасибо также тексту "Planescape: Torment - Novelization" за авторством, вроде как, Chris Avellone and Colin McComb.'

    snowinmars 'По итогу, я ставлю простой вопрос: а вот всё это, что я описал - оно сработает?'
    snowinmars 'Я пока не знаю. Я пытаюсь это узнать. Если ты мне поможешь в этом - будет супер.'
    snowinmars 'Мои контакты:\n  telegram: @snowinmars\n  email:snowinmars@yandex.ru'
    snowinmars 'Ссылка на репу с этим проектом:\n  https://github.com/snowinmars/PlanescapeTormentRmk'
    snowinmars 'Если хочешь оригинала - ссылка на прохождение:\n  https://wiki.aerie.ru/wiki/Категория:Прохождение_(PsT)'
    snowinmars 'Спасибо. Приятного перерождения.'

    menu:
        'Новая жизнь':
            jump new_life


label new_life:
    snowinmars 'Да, тут такое дело…'
    snowinmars 'Всё-таки я не могу полностью игнорировать билд: на нём завязаны диалоги, и завязаны сильно.'
    snowinmars 'Пользуясь правом технодемки, я делаю вжух -'
    $ introductionLogic.setup_new_life_as_mage()
    snowinmars '- и ты играешь магом. Если что - это база.'

    if introductionLogic.can_spoiler:
        snowinmars 'В технодемке есть только три этажа Морга. Но в эту локацию можно вернуться много раз по ходу игры, обладая дополнительными способностями.'
        snowinmars 'Так как я эти, хайлвл диалоги тоже перенёс сюда, было бы странно не предложить к ним доступа. Если хочешь - возьми.'

        menu:
            'Получить "История костей"':
                $ introductionLogic.setup_as_highlvl()
                snowinmars 'Вжух - ты умеешь разговаривать с духами.'
            'Не надо':
                snowinmars 'Одобряю аутентичность.'
    else:
        snowinmars 'Технодемка достаточно короткая.'

    snowinmars 'Вот теперь - правда.'

    menu:
        'Вспомнить':
            jump morte1_s0
